#
#                          Copyright (C) 2020 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#

include forgeutils/toolset.hsl
include ~/fsutil.hsl

local var src type list;
local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;

local var deps type string;

local var curr_toolset type string;

# -- lethe tool build

project lethe-tool : toolset $curr_toolset :
                        dependencies $deps : $src, $includes, $cflags, $libraries, $ldflags, "lethe";

lethe-tool.preloading() {
    $curr_toolset = get_app_toolset();
}

lethe-tool.prologue() {
    setup_lists();
    $deps = get_c_cpp_deps();
}

lethe-tool.epilogue() {
    run_tests();
}

# -- liblethe build

project liblethe : toolset $curr_toolset :
                      dependencies $deps : $src, $includes, $cflags, $libraries, $ldflags, "liblethe";

liblethe.preloading() {
    $curr_toolset = get_lib_toolset();
    $deps = get_c_cpp_deps();
}

liblethe.prologue() {
    setup_lists();
    $deps = get_c_cpp_deps();
    var s type int;
    $s = 0;
    while ($s < $src.count()) {
        var impfile type string;
        $impfile = $src.item($s);
        if ($impfile.match(".*(main\\.c|lethe_option\\.c)$") == 1) {
            var filename type string;
            $filename = filenamefrompath($impfile);
            $filename.replace(".c$", ".o");
            $filename = hefesto.sys.make_path("o", $filename);
            hefesto.sys.rm($filename);
            $src.del_index($s);
        }
        $s = $s + 1;
    }
}

liblethe.epilogue() {
    run_tests();
}

local function run_tests() : result type none {
    if (hefesto.sys.last_forge_result() == 0) {
        if (hefesto.sys.cd("test") != 1) {
            hefesto.sys.echo("BUILD ERROR: Test directory not found!\n");
            hefesto.project.abort(1);
        }
        var ftype type list;
        $ftype = hefesto.sys.get_option("forge-type");
        if (hefesto.sys.run("hefesto --forge-type=" + $ftype.item(0)) != 0) {
            hefesto.project.abort(1);
        }
        hefesto.sys.cd("..");
    }
}

local function setup_lists() : result type none {
    $src.ls(".*\\.c$");
    $includes = hefesto.sys.get_option("includes");
    $cflags = hefesto.sys.get_option("cflags");
    $libraries = hefesto.sys.get_option("libraries");
    $ldflags = hefesto.sys.get_option("ldflags");
}
